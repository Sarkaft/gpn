from Bio import SeqIO
import os
import pandas as pd

# conda install -c bioconda trf


rule all:
    input:
        "tair10.fa",
        "trf.bed",
        "first_seed/seqs/",


rule download_reference:
    output:
        "tair10.raw.fa",
    shell:
        "wget https://www.arabidopsis.org/download_files/Genes/TAIR10_genome_release/TAIR10_chromosome_files/TAIR10_chr_all.fas -O {output}"


rule clean_fasta:
    input:
        "tair10.raw.fa",
    output:
        "tair10.fa",
    run:
        records = []
        with open(input[0]) as input_handle:
            for record in SeqIO.parse(input_handle, "fasta"):
                print(record.id)
                if record.id in ["M", "chloroplast"]: continue
                record.id = "Chr" + record.id
                print(record.id)
                records.append(record)

        with open(output[0], "w") as output_handle:
            SeqIO.write(records, output_handle, "fasta")


rule run_trf:
    input:
        "tair10.fa",
    output:
        "trf.dat",
    shell:
        "trf {input} 2 7 7 80 10 30 12 -h -d -ngs > {output}"


rule reformat_trf:
    input:
        "trf.dat",
    output:
        "trf.bed",
    run:
        chromosome = None
        rows = []
        with open(input[0]) as f:
            for line in f:
                if line.startswith("@"):
                    chromosome = line.split(" ")[0].split("@")[1]
                    print(chromosome)
                    continue
                cols = line.split(" ")[:-3]
                rows.append([chromosome] + cols[:2] + ["trf"] + cols[2:])
        df = pd.DataFrame(rows)
        print(df)
        df.to_csv(output[0], sep="\t", header=False, index=False)


rule prepare_fasta:
    input:
        "tair10.fa",
    output:
        directory("first_seed/seqs/"),
    run:
        if not os.path.exists(output[0]):
            os.makedirs(output[0])
        with open(input[0]) as input_handle:
            for record in SeqIO.parse(input_handle, "fasta"):
                print(record.id)
                with open(os.path.join(output[0], record.id + ".fa"), "w") as output_handle:
                    SeqIO.write([record], output_handle, "fasta")

# cd first_seed
# R
# library(BSgenome)
# forgeBSgenomeDataPkg("BSgenome.Athaliana.TAIR.TAIR10FILT-seed")
# exit
# R CMD build BSgenome.Athaliana.TAIR.TAIR10FILT
# R CMD check BSgenome.Athaliana.TAIR.TAIR10FILT
